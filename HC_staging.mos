#HC mos script by ethtoja
#This HC script written for to support the developer's job: check the nodes status
#This scripts can be run from moshell with command e.g runx HC_staging.mos
#Supported node types: msrbs LTE, msrb GNB, msrbs dNURP,VDU, VCU CP, VCU UP
#you have to edit the $path_to_report variable in line 17 if you want different path to report file storage.
#Version 1.0 - First release
#Version 1.01 - 2022 oct 5 - The default report file directory is the user homedir.
#               Don't need to edit if it is ok for the user.

$version = 1.01
confb
gs
lt all
$report_to_file = report
$locked_cells_found = 0
unset $choice
$path_to_report = `echo $HOME`


################# Function declaration part below ######################

function cell_check
 !echo ' ' >> $report_to_file
 get EUtranCellFDD administrativestate 0
 if $nr_of_mos > 0
  $locked_cells_found = 1
 fi
 if $locked_cells_found = 1
  $choice = readinput(found locked LTE cells, do you want to unlock them and check ? )
 fi
 if $choice ~ y || $choice ~ Y
  $mo_to_unlock = EUtranCellFDD
  unlock_mo $mo_to_unlock
 else
  cell_check2
 fi
endfunc

function print_elements
 for $mo in st_group
  $temp = rdn($mo)
  !echo ' '$temp >> $path_to_report/$report_to_file
 done
endfunc

function cell_check2
 !echo '------------------------ LTE cell check ------------------------' >> $path_to_report/$report_to_file
 st EutranCellFDD dis
 if $nr_of_mos = 0
  !echo 'OK: All LTE cells are working' >> $path_to_report/$report_to_file
 else
  !echo 'The following LTE cells are disabled' >> $path_to_report/$report_to_file
  print_elements
 fi
 !echo '----------------------------------------------------------------' >> $path_to_report/$report_to_file
endfunc

function unlock_cells
 deb eutrancellfdd
 wait 10
 cell_check2
endfunc 

function mme_check
 !echo '------------------------ MME check ------------------------' >> $path_to_report/$report_to_file
 st mme ena
 $nr1 = $nr_of_mos
 if $nr1 = 1
  mme_check2
 fi
 if $nr1 > 1
  !echo 'WARNING : Two or more MME is enabled. In FsUE TC only one MME must be enabled. More enabled MMEs can cause problems during traffic tests!!!' >> $path_to_report/$report_to_file
  !echo '--------------------------------------------------------------' >> $path_to_report/$report_to_file
 fi  
 if $nr1 = 0
  !echo 'ERROR : No working MME on this TC. Without working MME the LTE cells can not be unlocked and enabled' >> $path_to_report/$report_to_file
  !echo '--------------------------------------------------------------' >> $path_to_report/$report_to_file
 fi
endfunc

function mme_check2
 ma working_mme ENodeBFunction=1,TermPointToMme= operationalState 1
 for $mo in working_mme
  get $mo termPointToMmeId > $id1
  $temp = ldn($mo)
  if $id1 ~ CSIM || $id1 ~ csim
   !echo 'OK:-- It seems only one MME is enabled and this MME is on the simulated CORE'  >> $path_to_report/$report_to_file
   !echo '----- MME name: '$temp  >> $path_to_report/$report_to_file
   !echo '-----------------------------------------------------------'>> $report_to_file
  else
   !echo 'WARNING: It seems only one MME is enabled, which is ok. But according to the MO name this is real CN MME' >> $path_to_report/$report_to_file
   !echo 'WARNING: If this TC is real CN connected then no problem, if it is an FsUE tc then the MME definition needs to be checked' >> $path_to_report/$report_to_file
   !echo 'WARNING: enabled mme is :'$temp >> $path_to_report/$report_to_file
   !echo '-----------------------------------------------------------' >> $path_to_report/$report_to_file
  fi  
 done
endfunc 

function rilink_check
 !echo '------------------------ RiLink Check ------------------------' >> $path_to_report/$report_to_file
 st rlink dis
 $nr1 = $nr_of_mos
 if $nr1 = 0
  !echo 'OK: All RiLinks are working.' >> $path_to_report/$report_to_file
 else
  !echo 'ERROR: Connection error detected between the msrbs and Radios.' >> $path_to_report/$report_to_file
  !echo 'This can cause cell outage in VDU and-or in LTE' >> $path_to_report/$report_to_file
  !echo 'List of faulty rilinks: ' >> $path_to_report/$report_to_file
  print_elements
 fi
 !echo '-------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
endfunc

function field_check
 !echo '------------------------ FieldReplaceableUnit Check ------------------------' >> $path_to_report/$report_to_file
 st field dis 
 $nr1 = $nr_of_mos
 if $nr1 = 0
  !echo 'OK: All HW element are working.' >> $path_to_report/$report_to_file
 else
  !echo 'ERROR: Some HW elements are not working.' >> $path_to_report/$report_to_file
  !echo 'This can cause cell outage in VDU and-or in LTE' >> $path_to_report/$report_to_file
  !echo 'List of disabled Fieldreplacable units: ' >> $path_to_report/$report_to_file
  print_elements
 fi
 !echo '-----------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
endfunc


function melink_check
 !echo ' ' >> $path_to_report/$report_to_file
 if $node_type = lte_node
  !echo '-------------------------------- ESS link - melink Check --------------------' >> $path_to_report/$report_to_file
 else
  !echo '-------------------------------- CA tunnel melink Check ---------------------' >> $path_to_report/$report_to_file
 fi
 st melink
  if $nr_of_mos = 0 && $node_type = lte_node
   !echo 'ERROR: ESS tunnel is missing' >> $path_to_report/$report_to_file
   !echo 'Please check:'  >> $path_to_report/$report_to_file
   !echo '- LTE ESS cell unlocked and enabled'   >> $path_to_report/$report_to_file
   !echo '- VDU ESS cell unlocked and enabled'   >> $path_to_report/$report_to_file
  fi   
  if $nr_of_mos = 0 && $node_type = 5G_mrsbs
   !echo 'ERROR: CA tunnel is missing' >> $path_to_report/$report_to_file
   !echo 'Please check:'  >> $path_to_report/$report_to_file
   !echo '- VDU cell unlocked and correct state'   >> $path_to_report/$report_to_file
   !echo '- 5G MSRBS cell is unlocked and correct state'   >> $path_to_report/$report_to_file
   !echo '- VCU CP NrCellRelation is defined'   >> $path_to_report/$report_to_file
  fi
  if $nr_of_mos = 1 && $node_type = lte_node
   !echo 'OK: ESS tunnel established' >> $path_to_report/$report_to_file
  fi
  if $nr_of_mos = 2 && $node_type = lte_node
   !echo 'OK: ESS tunnel established' >> $path_to_report/$report_to_file
   !echo 'OK: CA tunnel also present' >> $path_to_report/$report_to_file
  fi
  if $nr_of_mos = 1 && $node_type = 5G_mrsbs
   !echo 'OK: CA tunnel established' >> $path_to_report/$report_to_file
  fi
  
 !echo '-----------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 
 
endfunc

function NrSectorCarrier_check
 !echo '------------------------ NrSectorCarrier Check ------------------------'  >> $path_to_report/$report_to_file 
 ma nr_carrier NrSectorCarrier AdministrativeState 0
 if $nr_of_mos > 0
  $mo_to_unlock = NrSectorCarrier
  unlock_mo $mo_to_unlock
 fi
 st NrSectorCarrier dis
 if $nr_of_mos > 0
  !echo 'Disabled NrSectorCarrier is found, list below:' >>  $report_to_file
  print_elements
 else
  !echo 'OK : All NrSectorCarriers are working' >> $path_to_report/$report_to_file
 fi
 !echo '-----------------------------------------------------------------------'  >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
endfunc

function NrCellDU_check
 !echo '--------------------------- NrCellDU Check ----------------------------'  >> $path_to_report/$report_to_file
 ma nr_cells NrCellDU AdministrativeState 0
 if $nr_of_mos > 0
  $mo_to_unlock = NrCellDU
  unlock_mo $mo_to_unlock
 fi
 st NrCellDU dis
 if $nr_of_mos > 0
  !echo 'ERROR: Disabled NrCellDu is found ,listed below:'  >> $path_to_report/$report_to_file
  print_elements
 else
  !echo 'OK: All NrCellDus are Unlocked and Enabled' >> $path_to_report/$report_to_file  
 fi 
 check_nrcellDU_state
 !echo '-----------------------------------------------------------------------'  >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
endfunc

function VDU_check1
 get 0 userlabel > $site_name
 $report_to_file = $site_name_$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo 'VDU check on '$site_name' Date: '$date >> $path_to_report/$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 NrSectorCarrier_check
 NrCellDU_check

 !echo '---------------------- InterServiceLink check -------------------------'  >> $path_to_report/$report_to_file
 lt root
 lt all
 pr InterServiceLink=
 $no_InterServiceLink = $nr_of_mos
 if $nr_of_mos = 0
  !echo 'ERROR: No service link has been found. Possible reasons:'   >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
  !echo '- No ESS configured on the TC'
  !echo '- No ESS configured cell unlocked in VDU and-or in the LTE msrbs'   >> $path_to_report/$report_to_file
  !echo '- No Carrier Aggregation defined the in the VCU'   >> $path_to_report/$report_to_file
  !echo '- No Carrier Aggregated enabled in the VDU and-or in the other platform i.e msrbs 5G cell disabled'   >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
 fi
 st InterServiceLink= ena
 if $nr_of_mos = 1
  !echo 'WARNING: Only one working InterServiceLink has been found!'   >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
  !echo 'It is ok if only ESS capable TC and no Carrier Aggregation is defined' >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
 fi
 if $nr_of_mos = 2 
  !echo 'OK: ESS and Carrier Aggregation Tunnel is up' >> $path_to_report/$report_to_file
 fi
 st InterServiceLink= dis
 if $nr_of_mos > 0
  !echo 'ERROR: Disabled ServiceLink failure:' >> $path_to_report/$report_to_file
  print_elements
  !echo ' ' >> $path_to_report/$report_to_file
  !echo ' Possible reason: in case of ESS no LTE cell unlocked and enabled' >> $path_to_report/$report_to_file
 fi  
 
 !echo '-----------------------------------------------------------------------'  >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '---------------------- EP_F1C link check ------------------------------'  >> $path_to_report/$report_to_file
 ma EPF1C EP_F1C AdministrativeState 0 
 if $nr_of_mos > 0
  $mo_to_unlock = EP_F1C
  unlock_mo $mo_to_unlock
 fi
 st EP_F1C dis
 if $nr_of_mos > 0
  !echo 'ERROR: the EP_F1C link is disabled, which means that VCU-VDU connection is broken' >> $path_to_report/$report_to_file
  !echo ' ' >> report_to_file
  !echo 'Possible reason: VCU CP is not deployed or broken IP connection between VCU and VDU' >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
 else
  !echo 'OK: EP_F1C link is up, VDU is connected to the VCU' >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
 fi
 !echo '-----------------------------------------------------------------------'  >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '----------------------- Alarm check -----------------------------------'  >> $path_to_report/$report_to_file
 alarm_check
 !echo '-----------------------------------------------------------------------'  >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
endfunc

function check_nrcellDU_state
 st NrcellDu ena
 $state_check = good
 for $mo in st_group
  $temp = ldn($mo)
  get $mo cellState > $cell_state
  if $cell_state !~ 2
   !echo 'ERROR: '$temp' cell state is not ACTIVE, detected state: '$cell_state >> $path_to_report/$report_to_file
   $state_check = bad
  fi
  get $mo administrativeState > $admin1
  if $admin1 = 0
   !echo 'ERROR: '$temp' cell is not unlocked' >> $path_to_report/$report_to_file
   $state_check = bad
  fi
  get $mo serviceState > $service_state
  if $service_state  !~ IN_SERVICE
   !echo 'ERROR: '$temp' cell is not in_service state' >> $path_to_report/$report_to_file
   $state_check = bad
  fi  
 done
 if $state_check = good
   !echo 'OK: All VDU cells are in correct state' >> $path_to_report/$report_to_file
  else
   !echo ' ' >> $path_to_report/$report_to_file
   !echo 'ERROR: This VDU has faulty cells!' >> $path_to_report/$report_to_file
  fi
endfunc
  
 
function alarm_check
 
 alt
 if $nr_of_alarms = 0
  !echo 'The Node is alarm free' >> $path_to_report/$report_to_file
 else
  l+ $report_to_file
  alt
  l-
  !echo ' ' >> $path_to_report/$report_to_file 
 fi
endfunc
 
function unlock_mo
 !echo ' ' >> $path_to_report/$report_to_file
 !echo 'NOTIFICATION: Attempted to unlock locked '$mo_to_unlock >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 ma group_mo $mo_to_unlock administrativestate 0
 pr group_mo
 pv $mo_to_unlock
 for $mo in group_mo
  deb $mo
  wait 10
 done
 wait 10
  mr group_mo
endfunc

function dnurp_check1
 ma locked_l1bp NurpFunction=1,L1bp= administrativeState 0
 pr locked_l1bp
 if $nr_of_mos > 0
  $mo_to_unlock = l1bp
  unlock_mo $mo_to_unlock
 fi
 ma locked_nurpcell NurpFunction=1,Cell= administrativeState 0
 pr locked_nurpcell
 if $nr_of_mos > 0
  $mo_to_unlock = cell
  unlock_mo $mo_to_unlock
 fi
 wait 10
 check_l1bp
endfunc

function check_l1bp
 $skip_cell_test = no
 st l1bp dis
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '-------------------------------- L1BP Interface check --------------------' >> $path_to_report/$report_to_file
 if $nr_of_mos = 0
  !echo 'OK: L1BP interface works fine.' >> $path_to_report/$report_to_file
 fi
 if $nr_of_mos > 0
  !echo 'WARNING : Disabled L1BP interface list: '  >> $path_to_report/$report_to_file
  print_elements    
 fi
 st l1bp ena
 if $nr_of_mos = 0
  !echo 'ERROR: NO working L1BP has been found!'   >> $path_to_report/$report_to_file
  $skip_cell_test = yes  
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------- dNurp cell check -----------------------' >> $path_to_report/$report_to_file
 if $skip_cell_test = no
  check_dnurp_cell1
 else 
  !echo 'ERROR: Cell tests are skipped due to NO working L1BP' >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
  !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file 
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '-------------------------------- dNurp alarm check -----------------------' >> $path_to_report/$report_to_file
 !echo 'DNURP alarms:'  >> $path_to_report/$report_to_file
 alarm_check
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file 
endfunc

function check_dnurp_cell1
 $result = no_bouncing
 $ww = 5
 !echo ' '   >> $path_to_report/$report_to_file
 !echo 'dNurp cell state check' >> $path_to_report/$report_to_file
 !echo ' '   >> $path_to_report/$report_to_file
 for 6 
  st cell dis
  if $nr_of_mos > 0
   !echo 'The following cell was not stable/enabled during the check'  >> $path_to_report/$report_to_file
   print_elements
   !echo ' ' >> $path_to_report/$report_to_file
   $result_bouncing = detected
  fi
  wait $ww
  $ww = $ww - 1
 done
 !echo ' '   >> $path_to_report/$report_to_file
 st cell dis
 if $nr_of_mos = 0 && $result = no_bouncing
  !echo 'OK: dNURP cells are working and not bouncing'  >> $path_to_report/$report_to_file
 fi
 if $nr_of_mos = 0 && $result_bouncing = detected
  !echo 'WARNING: dNURP cells are working but seems they are bouncing'  >> $path_to_report/$report_to_file
  !echo 'WARNING: Suggest to lock/unlock VDU cells or redeploy VDU'  >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
 fi
 if $nr_of_mos > 0
  !echo 'ERROR: It seems the following cell is not working' >> $path_to_report/$report_to_file
  print_elements 
  !echo 'Suggest that the cells are deblocked in the VDU and msrbs 5G,'  >> $path_to_report/$report_to_file
  !echo 'Check that msrbs 5G and enabled and status in service'  >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
endfunc

function vcucp_check1
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '-------------------------------- VCU CP Interface towards VCU UP -----------------------' >> $path_to_report/$report_to_file
 st TermPointToGNBCUUP ena
 if $nr_of_mos = 0
  !echo 'ERROR: The VCU CP and VCU UP connection is broken' >> $path_to_report/$report_to_file
  !echo ' Possible reason: VCU UP not deployed or the the TLS communication is broken - ENM certficate issue, or network issue'  >> $path_to_report/$report_to_file
 else
  !echo 'OK: Interface towards VCU UP is working' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '---------------- VCU CP Interface towards RANS ---------------------------' >> $path_to_report/$report_to_file
 st TermPointToSdServer=
  if $nr_of_mos = 0
  !echo 'ERROR: The VCU CP and RANS connection is broken' >> $path_to_report/$report_to_file
  !echo ' Possible reason: RANS not deployed or the the TLS communication is broken - ENM certficate issue, or network issue'  >> $path_to_report/$report_to_file
 else
  !echo 'OK: Interface towards the RANS is working' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '-------------------------------- VCU CP Interface towards VDU or DU -----------------------' >> $path_to_report/$report_to_file
 st TermPointToGNBDU
 $defined_interface = $nr_of_mos
 if $nr_of_mos = 1
  !echo 'INFO: One interface found towards to the VDU or msrbs' >> $path_to_report/$report_to_file
  !echo ' It is ok, when no TDD msrbs connected to this VCU CP' >> $path_to_report/$report_to_file
 fi
 if $nr_of_mos = 2
  !echo 'INFO: Two interfaces found towards to the VDU and 5G NR TDD msrbs' >> $path_to_report/$report_to_file  
 fi
 $skip_test = no
 if $nr_of_mos = 0
  !echo 'ERROR: There is no interface presents towards any VDU - msrbs gnb' >> $path_to_report/$report_to_file
  !echo ' Possible reason: the VDU is not deployed, F1 is not unlocked , termpoint in the msrbs is locked, nw issue' >> $path_to_report/$report_to_file
  $skip_test = yes
 fi
 
 st TermPointToGNBDU ena
 if $nr_of_mos = $defined_interface && $skip_test = no
  !echo 'OK: All defined interface is enabled and working' >> $path_to_report/$report_to_file
 fi
 if $nr_of_mos < $defined_interface && $skip_test = no && $nr_of_mos > 0
  !echo 'WARNING: Not all defined interface are working' >> $path_to_report/$report_to_file
  !echo ' Working Interface below: ' >> $path_to_report/$report_to_file
  print_elements
  st TermPointToGNBDU dis
  !echo ' Disabled Interface below: ' >> $path_to_report/$report_to_file
  print_elements
 fi
 if $skip_test = no && $nr_of_mos = 0
  !echo 'ERROR: No working interface found' >> $path_to_report/$report_to_file
  !echo 'Disabled interface list:' >> $path_to_report/$report_to_file
  st TermPointToGNBDU dis
  print_elements
  !echo ' ' >> $path_to_report/$report_to_file
  !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 fi
 
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '-------------------------------- X2 towars ENB check ----------------------' >> $path_to_report/$report_to_file
 $x2_defined = yes
 pr TermPointToENB=
 if $nr_of_mos = 0
  !echo 'ERROR: No X2 interface defined towards the LTE ENB' >> $path_to_report/$report_to_file
  !echo ' Possible reason: locked TermpointToGNB in LTE node' >> $path_to_report/$report_to_file
  !echo ' Missing TermPointToGNB in the LTE node' >> $path_to_report/$report_to_file 
  $x2_defined = no
 fi
 st TermPointToENB dis
 if $nr_of_mos = 0 && $x2_defined = yes
  !echo 'OK: X2 interface towards ENB is working, connection towards LTE is ok.' >> $path_to_report/$report_to_file
 else
  !echo 'ERROR: the X2 interface towards LTE is down' >> $path_to_report/$report_to_file
  !echo ' List of x2 interface in malfunction:'  >> $path_to_report/$report_to_file
  print_elements
  !echo ' '  >> $path_to_report/$report_to_file
  !echo ' Possible reason: locked TermpointToGNB in LTE node' >> $path_to_report/$report_to_file
  !echo ' 5G NR cells are not operating or locked' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '-------------------------- EUtranCellRelation Check ----------------------' >> $path_to_report/$report_to_file
 ma ess1 EUtranCellRelation= essEnabled true
 pr ess1
 if $nr_of_mos = 0
  !echo 'ERROR: NO ESS relation defined in the VCU CP. Without it you can not test ESS' >> $path_to_report/$report_to_file 
  !echo ' ' >> $path_to_report/$report_to_file
 else
  !echo 'OK: ESS relation is defined in the VCU CP node' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '---------------------------- LTE cells in VCU CP -------------------------' >> $path_to_report/$report_to_file
 ma external_lte_cells ExternalEUtranCell=
 $lte_defined = yes
 pr external_lte_cells
 $ext_lte_num = $nr_of_mos
 if $nr_of_mos = 0
  !echo 'ERROR:No LTE cell detected by the VCU' >> $path_to_report/$report_to_file
  !echo ' You can not test ESS with this setup' >> $path_to_report/$report_to_file
  $lte_defined = no
 fi
 st ExternalEUtranCell= ena
 if $nr_of_mos = $ext_lte_num &&  $lte_defined = yes
  !echo 'OK: The LTE cell seems fine in the VDU' >> $path_to_report/$report_to_file
 else
  st ExternalEUtranCell= dis
  !echo 'WARNING: The following LTE cells are not in correct state' >> $path_to_report/$report_to_file
  print_elements
  !echo '' >> $path_to_report/$report_to_file
  !echo 'Possible reason: Need to check in the LTE node why it is down' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo '-------------------------------- NrCellCU check -----------------------' >> $path_to_report/$report_to_file
 pr nrcellcu=
 if $nr_of_mos = 0  
  !echo 'ERROR: No NrCellCU present in the VCU CP' >> $path_to_report/$report_to_file
  !echo ' Possible reason: Interface down towards the VDU-msrbs GNB , vdu not deployed' >> $path_to_report/$report_to_file
 else 
  vcucp_check2
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
endfunc

function vcucp_check2
 pr GNBCUCPFunction=.,NRCellCU=
 $number_of_nrcellcu = $nr_of_mos
 ma active_nrcellcu GNBCUCPFunction=.,NRCellCU= cellState 2
 mr active_nrcellcu GNBCUCPFunction=.,NRCellCU= serviceState !1
 pr active_nrcellcu
 if $nr_of_mos = $number_of_nrcellcu
  !echo 'OK: All NrCellCU is in good status.'  >> $path_to_report/$report_to_file
  !echo ' Note: pay attention for the VCU CP - VDU, msrbs interface' >>  $report_to_file
  !echo ' If not all interfaces is up then not all cell will appear as NrCellCu'  >>  $report_to_file
 fi
 mr st_group
 ma st_group GNBCUCPFunction=.,NRCellCU= cellState !2
 ma st_group GNBCUCPFunction=.,NRCellCU= serviceState !1
 pr st_group
 if $nr_of_mos > 0
  !echo 'ERROR: List of NrCellCU which are on wrong state:' >> $path_to_report/$report_to_file
  print_elements
  !echo ' Possible reason: needs unlock on NrCellDu in VDU or in msrbs 5G NR' >> $path_to_report/$report_to_file
 fi
 $nrcell_relation_defined = yes
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo '------------------------------ NrCellRelation check ----------------------' >> $path_to_report/$report_to_file
 pr NRCellRelation=
 if $nr_of_mos = 0
  $nrcell_relation_defined = no
  !echo ' ' >> $path_to_report/$report_to_file
  !echo 'WARNING: NO NrCellRelation defined ' >> $path_to_report/$report_to_file
  !echo ' You can not test NR Carrier Aggregation in this TC' >> $path_to_report/$report_to_file
 fi
 ma Aggregation_cells NRCellRelation= sCellCandidate 1
 pr Aggregation_cells
 $configured_fog_aggregation = $nr_of_mos
 if $nr_of_mos = 0 &&  $nrcell_relation_defined = yes
  !echo ' ' >> $path_to_report/$report_to_file
  !echo ' WARNING: No correctly parametered NrCellRelation has been found for CA'  >> $path_to_report/$report_to_file
 fi
 if $nrcell_relation_defined = yes & configured_fog_aggregation > 0
  aggregation_check2
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo '--------------------------- VCU CP alarm check ---------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 alarm_check
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
endfunc

function aggregation_check2
 !echo ' ' >> $path_to_report/$report_to_file
 $all_ok = yes
 for $mo in Aggregation_cells
  $temp = ldn($mo)
  get $mo caStatusActive > $ca_status
  if $ca_status = false
   !echo 'ERROR: '$temp >> $path_to_report/$report_to_file
   !echo ' CA is defined but not working' >> $path_to_report/$report_to_file
   $all_ok = no
  else
   !echo $temp 'OK: CA is defined and seems to be working'c
  fi
  if $all_ok = yes
   !echo 'OK: It seems the CA is working fine.' >> $path_to_report/$report_to_file
  else
   !echo 'WARNING: found anomalies' >> $path_to_report/$report_to_file
   !echo ' Make sure the cells are enabled and in service.'  >> $path_to_report/$report_to_file
  fi
 done
endfunc

function vcu_up_check1
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '-------------------------------- VCU UP Interface towards VCU CP -----------------------' >> $path_to_report/$report_to_file
 st e1 ena
 if $nr_of_mos = 1
  !echo ' ' >> $path_to_report/$report_to_file
  !echo 'OK: Interface towards VCU CP is ok.' >> $path_to_report/$report_to_file
 else
  !echo ' ' >> $path_to_report/$report_to_file
  !echo 'ERROR: The VCU UP and VCU CP connection is broken' >> $path_to_report/$report_to_file
  !echo ' Possible reason: VCU CP not deployed or the the TLS communication is broken - ENM certficate issue, or network issue'  >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '---------------- VCU UP Interface towards RANS ---------------------------' >> $path_to_report/$report_to_file
 st TermPointToSdServer=
 if $nr_of_mos = 0
  !echo 'ERROR: The VCU UP and RANS connection is broken' >> $path_to_report/$report_to_file
  !echo ' Possible reason: RANS not deployed or the the TLS communication is broken - ENM certficate issue, or network issue'  >> $path_to_report/$report_to_file
 else
  !echo 'OK: Interface towards the RANS is working' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 !echo '--------------------- VCU UP DL aggregation check ------------------------' >> $path_to_report/$report_to_file
 ma temp_1 . dcDlAggAllowed false
 pr temp_1
 $p_set = ok
 if $nr_of_mos = 3
  !echo 'OK: The aggregation paramters are OK for Validation tests' >> $path_to_report/$report_to_file
 else
  !echo 'WARNING: Aggregation parameters are not correct. User asked for correction' >> $path_to_report/$report_to_file
  $p_set = NOT_OK
 fi
 $answer = N
 if $p_set = NOT_OK
  $answer = readinput(DO you want to correct aggregation parameters? )
 fi
 if $answer ~ y || $answer ~ Y && $p_set = NOT_OK 
  set . dcDlAggAllowed false
  $p_set = ok
  !echo 'OK: Parameters are corrected.' >> $path_to_report/$report_to_file
 fi 
 if $p_set = NOT_OK && $answer !~ y || $answer !~ Y 
  !echo 'WARNING: Parameters are NOT corrected.' >> $path_to_report/$report_to_file
 fi 
 mr temp_1
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 
 !echo '------------------------- VCU UP alarm check -----------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 alarm_check
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
endfunc

function sector_check
 !echo '----------------------- SectorEquipemnt check ----------------------------' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 ma locked_sectors SectorEquipmentFunction administrativestate 0
 pr locked_sectors
 if $nr_of_mos > 0
  $mo_to_unlock = SectorEquipmentFunction
  unlock_mo $mo_to_unlock
 fi
 st SectorEquipmentFunction dis
 if $nr_of_mos > 0
  !echo 'ERROR: Disabled sector's list: ' >> $path_to_report/$report_to_file
  !echo ' ' >> $path_to_report/$report_to_file
  print_elements
 fi
 pr SectorEquipmentFunction=
 $nr_of_sectors = $nr_of_mos
 st SectorEquipmentFunction ena
 if $nr_of_sectors = $nr_of_mos
  !echo 'OK: Sector status is ok.' >> $path_to_report/$report_to_file
 fi
 !echo ' ' >> $path_to_report/$report_to_file
 !echo '--------------------------------------------------------------------------' >> $path_to_report/$report_to_file
 mr locked_sectors
endfunc  
################# Function declaration part END ######################
 
################# Main routine starts below ######################
  
################## Enodeb check part ######################

pr  ENodeBFunction=1
if $nr_of_mos > 0
 $node_type = lte_node
 $date = `date "+%Y-%m-%d %H:%M:%S"`
 get 0 userlabel > $site_name
 $report_to_file = $site_name_$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo 'MSRBS check on '$site_name' Date: '$date >> $path_to_report/$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 rilink_check
 field_check
 sector_check
 mme_check
 cell_check
 melink_check
 !echo ' ' >> $path_to_report/$report_to_file
 alarm_check
fi

  
################## VDU check part ######################
pr EP_F1C
if $nr_of_mos > 0
 $date = `date "+%Y-%m-%d %H:%M:%S"`
 VDU_check1
fi

################## dNurp check part ######################
pr L1bp=
if $nr_of_mos > 0
 $date = `date "+%Y-%m-%d %H:%M:%S"`
 get 0 userlabel > $site_name
 $report_to_file = $site_name_$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo 'dNurp check on '$site_name' Date: '$date >> $path_to_report/$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
  dnurp_check1
fi

################## VCU CP part ######################
pr nrcellcu=
if $nr_of_mos > 0
 $date = `date "+%Y-%m-%d %H:%M:%S"`
 get 0 userlabel > $site_name
 $report_to_file = $site_name_$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo 'VCU CP check on '$site_name' Date: '$date >> $path_to_report/$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
  vcucp_check1
fi

################## VCU UP part ######################
pr ServiceDefinition=
if $nr_of_mos > 0
 $date = `date "+%Y-%m-%d %H:%M:%S"`
 get 0 userlabel > $site_name
 $report_to_file = $site_name_$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo 'VCU UP check on '$site_name' Date: '$date >> $path_to_report/$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
  vcu_up_check1
fi

################## msrs gnb part ######################
$msrbs_gnb1 = no
$msrbs_gnb2 = no
pr FieldReplaceableUnit=1
if $nr_of_mos > 0
 $msrbs_gnb1 = yes
fi
pr NrCellDu=
if $nr_of_mos > 0
 $msrbs_gnb2 = yes
fi
if $msrbs_gnb1 = yes && $msrbs_gnb2 = yes
 $node_type = 5G_mrsbs
 $date = `date "+%Y-%m-%d %H:%M:%S"`
 get 0 userlabel > $site_name
 $report_to_file = $site_name_$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo 'GNB MRBS check on '$site_name' Date: '$date >> $path_to_report/$report_to_file
 !echo '**********************************************************************************' >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
 rilink_check
 field_check
 sector_check
 NrSectorCarrier_check
 NrCellDU_check
 melink_check
 !echo '----------------------- Alarm check -----------------------------------'  >> $path_to_report/$report_to_file
 alarm_check
 !echo '-----------------------------------------------------------------------'  >> $path_to_report/$report_to_file
 !echo ' ' >> $path_to_report/$report_to_file
fi

!echo '************************** REPORT END ********************************************'  >> $path_to_report/$report_to_file
!echo ' '  >> $path_to_report/$report_to_file
!echo 'Made by script version: '$version   >> $path_to_report/$report_to_file
!echo ' '  >> $path_to_report/$report_to_file
!cat $path_to_report/$report_to_file
$choice = readinput(Do you want to to keep this report file ? )
if $choice ~ y || $choice ~ Y
 !echo report stored
else
 !rm $path_to_report/$report_to_file
fi
